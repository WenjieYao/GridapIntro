{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "1-element Array{String,1}:\n",
       " \"demo.vtu\""
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Gridap\n",
    "using SparseArrays\n",
    "using SpecialFunctions\n",
    "\n",
    "# Geometry parameters\n",
    "L = 4.0          # Length of the square area\n",
    "d_pml = 0.8      # Thickness of the PML\n",
    "resolution = 20  # Number of cells per wavelength\n",
    "\n",
    "# Physical parameters\n",
    "λ = 1.0          # Wavelength (arbitrary unit)\n",
    "k = 2*π/λ        # Wave number \n",
    "δ = λ/resolution # Gaussian point source width\n",
    "\n",
    "# PML parameters\n",
    "R = 1e-4         # Tolerence for PML reflection \n",
    "σ = -3/4*log(R)/d_pml\n",
    "\n",
    "# PML coordinate stretching functions\n",
    "function s_PML(x,σ,k,L,d_pml)\n",
    "    u = abs.(Tuple(x)).-L./2\n",
    "    return @. ifelse(u > 0,  1-(1im*σ/k)*(u/d_pml)^2, $(1.0+0im))\n",
    "end\n",
    "\n",
    "function Λ(x,σ,k,L,d_pml)  \n",
    "    s_x,s_y = s_PML(x,σ,k,L,d_pml)\n",
    "    return TensorValue(1/s_x,0,0,1/s_y) \n",
    "end\n",
    "\n",
    "# Create Cartesian mesh and the model\n",
    "# (L+2d_pml)*(L+2d_pml) domain size with the center (0,0)\n",
    "domain = (-L/2-d_pml,L/2+d_pml,-L/2-d_pml,L/2+d_pml)\n",
    "Nc = round(Int,(L+2d_pml)/λ*resolution)     # Number of cells per side\n",
    "cells = (Nc,Nc)\n",
    "model = CartesianDiscreteModel(domain,cells)\n",
    "\n",
    "# Generate triangulation and quadrature from model \n",
    "trian = Triangulation(model)\n",
    "degree = 2\n",
    "quad = CellQuadrature(trian,degree)\n",
    "\n",
    "# Test and trial finite element function space\n",
    "order = 1\n",
    "V = TestFESpace(\n",
    "  reffe=:Lagrangian,\n",
    "  order=1,\n",
    "  valuetype=Float64,\n",
    "  model=model,\n",
    "  conformity=:H1,\n",
    "  dirichlet_tags=\"boundary\")\n",
    "\n",
    "U = TrialFESpace(V,0)\n",
    "\n",
    "\n",
    "# Define the theoretic model\n",
    "# Bilinear term\n",
    "a(u,v,k,σ,L,d_pml) = ((x->Λ(x,σ,k,L,d_pml))⋅∇(v))⊙((x->Λ(x,σ,k,L,d_pml))⋅∇(u)) - k^2*v⊙u\n",
    "# Source term (Gaussian point source approximation at center)\n",
    "f(x,δ) = 1/(2π*δ^2)*exp(-(norm(x)/δ)^2/2)\n",
    "b_Ω(v,δ) = v*(x->f(x,δ))\n",
    "\n",
    "# Assemble the matrix and solve for the field\n",
    "t_Ω = AffineFETerm((u,v)->a(u,v,k,σ,L,d_pml),v->b_Ω(v,δ),trian,quad)\n",
    "op = AffineFEOperator(SparseMatrixCSC{ComplexF64,Int},Vector{ComplexF64},U,V,t_Ω)\n",
    "A = get_matrix(op)\n",
    "b = get_vector(op)\n",
    "uvec = A\\b\n",
    "\n",
    "# Analytical solution for a magnetic dipole\n",
    "function H_t(x,λ)\n",
    "    kr = 2*π/λ*norm(x)\n",
    "    return -0.25im*hankelh2(0,kr)\n",
    "end\n",
    "\n",
    "b_theory(v) = v*(x->H_t(x,λ))\n",
    "t_theory = AffineFETerm((u,v)->(u*v),b_theory,trian,quad)\n",
    "op_t = AffineFEOperator(SparseMatrixCSC{ComplexF64,Int},Vector{ComplexF64},U,V,t_theory)\n",
    "A_t = get_matrix(op_t)\n",
    "b_t = get_vector(op_t)\n",
    "u_t = A_t\\b_t\n",
    "\n",
    "# Compare the relative difference\n",
    "# Since there will always be a large difference at the center \n",
    "# and in PML, we use x/(1+x) to show the difference\n",
    "Gridap.outer(a::Number,b::Number) = a*b\n",
    "@law myabs(x) = abs(x)/(1+abs(x))\n",
    "diff = myabs(FEFunction(U,uvec)-FEFunction(U,u_t))\n",
    "\n",
    "# Save to file and view\n",
    "writevtk(trian,\"demo\",cellfields=[\"Real\"=>FEFunction(U,real(uvec)),\n",
    "        \"Imag\"=>FEFunction(U,imag(uvec)),\n",
    "        \"Norm\"=>FEFunction(U,sqrt.(real(uvec).^2+imag(uvec).^2)),\n",
    "        \"Real_t\"=>FEFunction(U,real(u_t)),\n",
    "        \"Imag_t\"=>FEFunction(U,imag(u_t)),\n",
    "        \"Norm_t\"=>FEFunction(U,sqrt.(real(u_t).^2+imag(u_t).^2)),\n",
    "        \"Difference\"=>diff])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.1",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
